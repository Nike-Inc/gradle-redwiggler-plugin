plugins {
    id 'net.saliman.cobertura' version '2.2.7'
    id "com.github.nwillc.buildinfo" version "0.1.2"
}

repositories {
    mavenLocal()
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'groovy'

group = 'com.nike.redwiggler'

apply from: file('gradle/release.gradle')
apply from: file('gradle/convention.gradle')
apply from: file('gradle/check.gradle')

def redwigglerVersion = "0.5.1493753930631"

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile "org.slf4j:jcl-over-slf4j:1.7.5"
    compile "org.slf4j:slf4j-simple:1.7.5"
    compile "com.nike.redwiggler:redwiggler-common:$redwigglerVersion"
}

tasks.withType(JavaCompile) {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
}

buildinfo {
    output = "$buildDir/generated-resources/com/nike/redwiggler/plugin/build.json"
    custom["redwigglerVersion"] = redwigglerVersion;
}

task createBuildInfoDir << {
  (new File(buildinfo.output)).getParentFile().mkdirs()
}

buildInfo.dependsOn(createBuildInfoDir)

processResources.dependsOn buildInfo

sourceSets {
  main {
    output.dir("$buildDir/generated-resources", builtBy: buildInfo)
  }
}
