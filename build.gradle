plugins {
    id 'net.saliman.cobertura' version '2.2.7'
    id "com.github.nwillc.buildinfo" version "0.1.2"
}

repositories {
    jcenter()
    jcenter {
      artifactUrls = ["https://dl.bintray.com/nike/maven/"]
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'groovy'

group = 'com.nike.redwiggler.gradle'

apply from: file('gradle/release.gradle')
apply from: file('gradle/convention.gradle')
apply from: file('gradle/check.gradle')

def redwigglerVersion = "0.5.0"

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile "org.slf4j:jcl-over-slf4j:1.7.5"
    compile "org.slf4j:slf4j-simple:1.7.5"
    compile 'org.scala-lang:scala-library:2.12.3'
    compile "com.nike.redwiggler:redwiggler-core_2.12:$redwigglerVersion"
    compile "com.nike.redwiggler:redwiggler-swagger_2.12:$redwigglerVersion"
    compile "com.nike.redwiggler:redwiggler-reports-html_2.12:$redwigglerVersion"
}

tasks.withType(JavaCompile) {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
}

buildinfo {
    output = "$buildDir/generated-resources/com/nike/redwiggler/gradle/build.json"
    custom["redwigglerVersion"] = redwigglerVersion;
}

task createBuildInfoDir << {
  (new File(buildinfo.output)).getParentFile().mkdirs()
}

buildInfo.dependsOn(createBuildInfoDir)

processResources.dependsOn buildInfo

sourceSets {
  main {
    output.dir("$buildDir/generated-resources", builtBy: buildInfo)
  }
}
